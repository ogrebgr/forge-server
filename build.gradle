ext {
    forge_server_version = '9.0.1'
}

group 'com.bolyartech.forge'
version forge_server_version


apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    compile 'jakarta.servlet:jakarta.servlet-api:4.0.2'
    compile 'org.slf4j:slf4j-api:1.7.32'
    compile 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    compile 'com.google.guava:guava:30.1.1-jre'
    compile  group: 'com.mchange', name: 'c3p0', version: '0.9.5'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

tasks.withType(Javadoc).all {
    enabled = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.bolyartech.forge'
            artifactId 'forge-server'
            version forge_server_version
            artifact sourcesJar
        }
    }
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Forge Server'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Forge server is the base for the server part of the Forge framework'
                url 'https://github.com/ogrebgr/forge-server'

                scm {
                    connection 'scm:git:https://github.com/ogrebgr/forge-server.git'
                    developerConnection 'scm:git:https://github.com/ogrebgr/forge-server.git'
                    url 'https://github.com/ogrebgr/forge-server'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ogremir'
                        name 'Ognyan Bankov'
                        email 'bankov@bolyartech.com'
                    }
                }
            }
        }
    }
}


archivesBaseName = "forge-server"